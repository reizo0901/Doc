@echo off

rem ################################################################################
rem イベントログのアーカイブファイルを共有ディスクへ移動
rem 過去１年以上前のイベントログのアーカイブファイルを共有ディスクから削除
rem ################################################################################

rem ################################################################################
rem 環境変数
rem ################################################################################

rem 実行ログファイル格納場所(例：c:\tool)
set logpath=c:\tool
rem 実行ログファイル名(先頭)
set logfirst=evtxjob
rem 実行ログファイル名生成
set yyyyMMdd=%date:/=%
set hhmmss=%time::=%
set hhmmss=%hhmmss:~0,6%
set logfile=%logpath%\%logfirst%_%yyyyMMdd%_%hhmmss%.log

rem 実行ログ保持期間[日数](例：7)
set lperiod=7
rem アーカイブログ保持期間[日数](例：366)
set aperiod=366
rem コピー元Windowsイベントログアーカイブファイル格納場所(例：c:\windows\system32\winevt\logs)
set objectpath=d:\windows\system32\winevt\logs
rem コピー先Windowsイベントログアーカイブファイル格納フォルダ名(例：winevt\logs)
set targetfolder=winevt\logs
rem コピー先共有ドライブ文字(例：g)
set sharedv=g

rem ################################################################################
rem メイン処理
rem ################################################################################

echo [%date% %time%][%computername%]イベントログアーカイブファイル移動・削除処理[処理を開始しました]>>%logfile%

rem ################################################################################
rem 実行ログ削除処理
rem ################################################################################

echo [%date% %time%][%computername%]実行ログ削除処理[開始]>>%logfile%
forfiles /P %logpath% /D -%lperiod% /M %logfirst%_*.log /c "cmd /c del -f -q @path&&echo [削除][成功] @path>>%logfile%||echo [削除][失敗] @path>>%logfile%"
echo [%date% %time%][%computername%]実行削除処理[終了]>>%logfile%

rem ################################################################################
rem 共有ディスク接続文字列作成
rem ################################################################################

rem 共有ディスクがマウントされていない場合は、処理を終了する
if not exist "%sharedv%:\" (
    echo [%date% %time%][%computername%]共有ディスク[%sharedv%が存在しないため処理を終了しました。]>>%logfile%
    goto BATEND
)
rem 共有フォルダパス文字列
set targetpath=%sharedv%:\%targetfolder%\%computername%
echo 共有ディスク処理パス[%targetpath%]>>%logfile%

rem ################################################################################
rem アーカイブファイル削除処理
rem ################################################################################

echo [%date% %time%][%computername%]アーカイブファイル削除処理[開始]>>%logfile%
rem 指定日数より前の古いアーカイブファイルの削除を行う。
forfiles /P %targetpath% /D -%aperiod% /M archive-*.evtx /C "cmd /c del -f -q @path&&echo [削除][成功]@file>>%logfile%||echo [削除][成功]@file>>%logfile%"
echo [%date% %time%][%computername%]アーカイブファイル削除処理[終了]>>%logfile%

rem ################################################################################
rem アーカイブファイル移動処理
rem ################################################################################

echo [%date% %time%][%computername%]アーカイブファイル移動処理[開始]>>%logfile%
rem 対象のフォルダのアーカイブファイルのみの移動を行う
dir /A-D /B %objectpath%\archive-*.evtx | find /c /v "" | findstr /r "^0$">>%logfile% 
if "%errorlevel%"=="1" (
    forfiles /P %objectpath% /M archive-*.evtx /C "cmd /c move /y @path %targetpath%\@file&&echo [移動][成功]@file>>%logfile%||echo [移動][失敗]@file>>%logfile%"
) else (
    echo 移動対象となるアーカイブログが存在しません。
)
echo [%date% %time%][%computername%]アーカイブファイル移動処理[終了]>>%logfile%

:BATEND
echo [%date% %time%][%computername%]イベントログアーカイブファイル移動・削除処理[終了]>>%logfile%
rem exit /b 0
